plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {

	// Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	// gRPC
	implementation 'io.grpc:grpc-netty-shaded:1.50.0'
	implementation 'io.grpc:grpc-protobuf:1.50.0'
	implementation 'io.grpc:grpc-stub:1.50.0'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	// Jedis
	implementation 'redis.clients:jedis:4.2.0'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis:3.1.2'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Prometheus
	implementation 'io.micrometer:micrometer-registry-prometheus:1.12.3'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}


protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.24.0"
	}
	plugins {
		grpc {
			artifact = 'io.grpc:protoc-gen-grpc-java:1.50.0'
		}
	}
	generateProtoTasks {
		all().each { task ->
			task.plugins {
				grpc {}
			}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/java',
					'build/generated/source/proto/main/grpc'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
